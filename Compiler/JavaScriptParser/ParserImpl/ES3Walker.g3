tree grammar ES3Walker ;

options
{
	ASTLabelType = CommonTree ;
	tokenVocab = ES3 ;
	k = 4;
}

@namespace { Xebic.Parsers.ES3 }

@header {
using System;
using TypeScriptModel.TypeSystem;
using Saltarelle.Compiler.JSModel.Statements;
using Saltarelle.Compiler.JSModel.Expressions;
using TypeScriptModel.TypeSystem.Elements;
using TypeScriptModel.Elements;
using TypeScriptModel.Elements.ClassMembers;
using System.Globalization;
}

@members {	
	public override void ReportError(RecognitionException e) { throw e; }

	public string UnescapeStringLiteral(string orig) {
		if (orig.IndexOf('\\') == -1)
			return orig.Substring(1, orig.Length - 2);

		var result = new System.Text.StringBuilder();
		for (int i = 1; i < orig.Length - 1; i++) {
			char ch = orig[i];
			if (ch == '\\') {
				ch = orig[++i];
				if (ch == '\'')
					result.Append('\'');
				else if (ch == '\"')
					result.Append('\"');
				else if (ch == 'b')
					result.Append('\b');
				else if (ch == 'f')
					result.Append('\f');
				else if (ch == 'n')
					result.Append('\n');
				else if (ch == 'r')
					result.Append('\r');
				else if (ch == 't')
					result.Append('\t');
				else if (ch == 'v')
					result.Append('\v');
				else if (ch == '0')
					result.Append('\0');
				else if (ch == '/')
					result.Append('/');
				else
					result.Append('\\').Append(ch);	// TODO: More escape sequences.
			}
			else
				result.Append(ch);
		}
		return result.ToString();
	}

	public JsConstantExpression ParseRegexpLiteral(string orig) {
		var pattern = new System.Text.StringBuilder();
		int i = 1;
		for (; i < orig.Length - 1; i++) {
			char ch = orig[i];
			if (ch == '\\') {
				ch = orig[++i];
				if (ch == '\'')
					pattern.Append('\'');
				else if (ch == '\"')
					pattern.Append('\"');
				else if (ch == 'b')
					pattern.Append('\b');
				else if (ch == 'f')
					pattern.Append('\f');
				else if (ch == 'n')
					pattern.Append('\n');
				else if (ch == 'r')
					pattern.Append('\r');
				else if (ch == 't')
					pattern.Append('\t');
				else if (ch == 'v')
					pattern.Append('\v');
				else if (ch == '0')
					pattern.Append('\0');
				else if (ch == '/')
					pattern.Append('/');
				else
					pattern.Append('\\').Append(ch);	// TODO: More escape sequences.
			}
			else if (ch == '/')
				break;
			else
				pattern.Append(ch);
		}

		string options = i < orig.Length - 1 ? orig.Substring(i + 1) : null;

		return JsExpression.Regexp(pattern.ToString(), options);
	}

	public int DecodeOctalNumber(string value) {
		int result = 0;
		for (int i = 0; i < value.Length; i++)
			result = result * 8 + (value[i] - '0');
		return result;
	}
}

public program returns [IList<TsSourceElement> result]
@init { $result = new List<TsSourceElement>(); }
	: (s = sourceElement { $result.Add(s); })*
	;

public sourceElement returns [TsSourceElement result]
    : ((e = statementElement)
    | (e = interfaceElement)
    | (e = classElement)
    | (e = moduleElement)
    | (e = exportDeclaration)
    | (e = ambientDeclaration)
    | (e = ambientFunctionDeclaration)
    | (e = importDeclaration))
    { $result = e; }
    ;

public statementElement returns [TsSourceElement result]
    : s = statement
    { $result = new TsStatementElement(s); }
    ;

public statement returns [JsStatement result]
	: (x=block
	| x=variableDeclaration
	| e=expression { x = new JsExpressionStatement(e); }
	| x=ifStatement
	| x=doStatement
	| x=whileStatement
	| x=forStatement
	| x=continueStatement
	| x=breakStatement
	| x=returnStatement
	| x=withStatement
	| x=labelledStatement
	| x=switchStatement
	| x=throwStatement
	| x=tryStatement
	| x=functionDeclaration
	| x=gotoStatement
	| x=yieldStatement
	| x=awaitStatement
	| SEMIC { x = new JsEmptyStatement(); } )
	{ $result = x; }
	;

block returns [JsStatement result]
@init { var stmts = new List<JsStatement>(); }
	: ^( BLOCK (s=statement { stmts.Add(s); })* )
	{ $result = new JsBlockStatement(stmts); }
	;

variableDeclaration returns [JsStatement result]
@init { var vars = new List<JsVariableDeclaration>(); }
	: ^( VAR ((i=identifier a=typeAnnotation? (ASSIGN e=expression)?) { vars.Add(new JsVariableDeclaration(i, e, a)); })+)
    { $result = new JsVariableDeclarationStatement(vars); }
	;

ifStatement returns [JsStatement result]
	: ^( IF x=expression a=statement b=statement? ) { $result = JsStatement.If(x, a, b); }
	;

doStatement returns [JsStatement result]
	: ^( DO s=statement x=expression ) { $result = JsStatement.DoWhile(x, s); }
	;

whileStatement returns [JsStatement result]
	: ^( WHILE x=expression s=statement ) { $result = JsStatement.While(x, s); }
	;

forStatement returns [JsStatement result]
	: ^(
	FOR 
	(
		  ( ^( FORSTEP { JsStatement init = null; } ( x=exprOptClause { init = x != null ? (JsStatement)new JsExpressionStatement(x) : new JsEmptyStatement(); } | d=variableDeclaration { init = d; } ) x=exprOptClause y=exprOptClause ) s=statement { $result = new JsForStatement(init, x, y, s); } )
		| ( ^( FORITER { bool isDeclared = false; } ( ^( EXPR i=identifier ) | ^( VAR i=identifier { isDeclared = true; } ) ) x=exprClause ) s=statement { $result = new JsForEachInStatement(i, x, s, isDeclared); } )
	)
	);

exprOptClause returns [JsExpression result]
	: ^( EXPR x=expression? ) { $result = x; }
	;

exprClause returns [JsExpression result]
	: ^( EXPR x=expression ) { $result = x; }
	;

continueStatement returns [JsStatement result]
	: ^( CONTINUE i=Identifier? ) { $result = JsStatement.Continue(i != null ? i.Text : null); }
	;

breakStatement returns [JsStatement result]
	: ^( BREAK i=Identifier? ) { $result = JsStatement.Break(i != null ? i.Text : null); }
	;

returnStatement returns [JsStatement result]
	: ^( RETURN x= expression? ) { $result = JsStatement.Return(x); }
	;

withStatement returns [JsStatement result]
	: ^( WITH x=expression s=statement ) { $result = JsStatement.With(x, s); }
	;

labelledStatement returns [JsStatement result]
	: ^( LABELLED Identifier s=statement ) { $result = JsStatement.Label($Identifier.Text, s); }
	;

switchStatement returns [JsStatement result]
@init { var sections = new List<JsSwitchSection>(); }
	: ^( SWITCH { var values = new List<JsExpression>(); List<JsStatement> stmts = null; } x=expression
	     ((
			d=defaultClause { values.Add(null); stmts = d; }
		  | c=caseClause { values.Add(c.Item1); stmts = c.Item2; }
         ) { if (stmts != null) { sections.Add(JsStatement.SwitchSection(values, stmts.Count == 1 ? JsStatement.EnsureBlock(stmts[0]) : JsStatement.Block(stmts))); values = new List<JsExpression>(); } } )*
	   ) { if (values.Count > 0) { sections.Add(JsStatement.SwitchSection(values, JsStatement.EmptyBlock)); } $result = JsStatement.Switch(x, sections); }
	;

defaultClause returns [List<JsStatement> result]
	: ^( DEFAULT (s=statement { $result = $result ?? new List<JsStatement>(); $result.Add(s); })* )
	;

caseClause returns [Tuple<JsExpression, List<JsStatement>> result]
@init { List<JsStatement> stmts = null; }
	: ^( CASE x=expression (s=statement { stmts = stmts ?? new List<JsStatement>(); stmts.Add(s); })* ) { $result = Tuple.Create(x, stmts); }
	;

throwStatement returns [JsStatement result]
	: ^( THROW x=expression ) { $result = JsStatement.Throw(x); }
	;

tryStatement returns [JsStatement result]
	: ^( TRY a=block b=catchClause? c=finallyClause? ) { $result = JsStatement.Try(a, b, c); }
	;
	
catchClause returns [JsCatchClause result]
	: ^( CATCH i=identifier b=block ) { $result = JsStatement.Catch(i, b); }
	;
	
finallyClause returns [JsStatement result]
	: ^( FINALLY b=block ) { $result = b; }
	;

debuggerStatement returns [JsStatement result]
	: DEBUGGER { $result = JsStatement.Debugger; }
	;

gotoStatement returns [JsStatement result]
	: ^( GOTO i=identifier ) { $result = JsStatement.Goto(i); }
	;

yieldStatement returns [JsStatement result]
	: ^( YIELD x=expression? ) { $result = JsStatement.Yield(x); }
	;

awaitStatement returns [JsStatement result]
	: ^( AWAIT x=expression y=identifier ) { $result = JsStatement.Await(x, y); }
	;

public expression returns [JsExpression result]
	: x=expr { $result = x; }
	| ^( CEXPR { var list = new List<JsExpression>(); } (x=expr { list.Add(x); })+ { $result = JsExpression.Comma(list); })
	;

expr returns [JsExpression result]
	: x=leftHandSideExpression { $result = x; }
	
	// Assignment operators
	| ^( ASSIGN a=expr b=expr { $result = JsExpression.Assign(a, b); } )
	| ^( MULASS a=expr b=expr { $result = JsExpression.MultiplyAssign(a, b); } )
	| ^( DIVASS a=expr b=expr { $result = JsExpression.DivideAssign(a, b); } )
	| ^( MODASS a=expr b=expr { $result = JsExpression.ModuloAssign(a, b); } )
	| ^( ADDASS a=expr b=expr { $result = JsExpression.AddAssign(a, b); } )
	| ^( SUBASS a=expr b=expr { $result = JsExpression.SubtractAssign(a, b); } )
	| ^( SHLASS a=expr b=expr { $result = JsExpression.LeftShiftAssign(a, b); } )
	| ^( SHRASS a=expr b=expr { $result = JsExpression.RightShiftSignedAssign(a, b); } )
	| ^( SHUASS a=expr b=expr { $result = JsExpression.RightShiftUnsignedAssign(a, b); } )
	| ^( ANDASS a=expr b=expr { $result = JsExpression.BitwiseAndAssign(a, b); } )
	| ^( XORASS a=expr b=expr { $result = JsExpression.BitwiseXorAssign(a, b); } )
	| ^( ORASS  a=expr b=expr { $result = JsExpression.BitwiseOrAssign(a, b); } )
	
	// Conditional operator
	| ^( QUE a=expr b=expr c=expr  { $result = JsExpression.Conditional(a, b, c); } )
	
	// Logical operators
	| ^( LOR        a=expr b=expr { $result = JsExpression.LogicalOr(a, b); } )
	| ^( LAND       a=expr b=expr { $result = JsExpression.LogicalAnd(a, b); } )
	
	// Binary bitwise operators
	| ^( AND        a=expr b=expr { $result = JsExpression.BitwiseAnd(a, b); } )
	| ^( OR         a=expr b=expr { $result = JsExpression.BitwiseOr(a, b); } )
	| ^( XOR        a=expr b=expr { $result = JsExpression.BitwiseXor(a, b); } )
	
	// Equality operators
	| ^( EQ         a=expr b=expr { $result = JsExpression.Equal(a, b); } )
	| ^( NEQ        a=expr b=expr { $result = JsExpression.NotEqual(a, b); } )
	| ^( SAME       a=expr b=expr { $result = JsExpression.Same(a, b); } )
	| ^( NSAME      a=expr b=expr { $result = JsExpression.NotSame(a, b); } )
	
	// Relational operator
	| ^( LT         a=expr b=expr { $result = JsExpression.Lesser(a, b); } )
	| ^( GT         a=expr b=expr { $result = JsExpression.Greater(a, b); } )
	| ^( LTE        a=expr b=expr { $result = JsExpression.LesserOrEqual(a, b); } )
	| ^( GTE        a=expr b=expr { $result = JsExpression.GreaterOrEqual(a, b); } )
	| ^( INSTANCEOF a=expr b=expr { $result = JsExpression.InstanceOf(a, b); } )
	| ^( IN         a=expr b=expr { $result = JsExpression.In(a, b); } )
	
	// Bitwise shift operator
	| ^( SHL        a=expr b=expr { $result = JsExpression.LeftShift(a, b); } )
	| ^( SHR        a=expr b=expr { $result = JsExpression.RightShiftSigned(a, b); } )
	| ^( SHU        a=expr b=expr { $result = JsExpression.RightShiftUnsigned(a, b); } )
	
	// Additive operators
	| ^( ADD        a=expr b=expr { $result = JsExpression.Add(a, b); } )
	| ^( SUB        a=expr b=expr { $result = JsExpression.Subtract(a, b); } )
	
	// Multipiclative operators
	| ^( MUL        a=expr b=expr { $result = JsExpression.Multiply(a, b); } )
	| ^( DIV        a=expr b=expr { $result = JsExpression.Divide(a, b); } )
	| ^( MOD        a=expr b=expr { $result = JsExpression.Modulo(a, b); } )
	
	// Unary operator
	| ^( DELETE a=expr { $result = JsExpression.Delete(a); } )
	| ^( VOID   a=expr { $result = JsExpression.Void(a); } )
	| ^( TYPEOF a=expr { $result = JsExpression.TypeOf(a); } )
	| ^( INC    a=expr { $result = JsExpression.PrefixPlusPlus(a); } )
	| ^( DEC    a=expr { $result = JsExpression.PrefixMinusMinus(a); } )
	| ^( POS    a=expr { $result = JsExpression.Positive(a); } )
	| ^( NEG    a=expr { $result = JsExpression.Negate(a); } )
	| ^( INV    a=expr { $result = JsExpression.BitwiseNot(a); } )
	| ^( NOT    a=expr { $result = JsExpression.LogicalNot(a); } )
	
	// Postfix operators
	| ^( PINC   a=expr { $result = JsExpression.PostfixPlusPlus(a); } )
	| ^( PDEC   a=expr { $result = JsExpression.PostfixMinusMinus(a); } )
	;

leftHandSideExpression returns [JsExpression result]
	: x=primaryExpression  { $result = x; }
	| x=newExpression      { $result = x; }
	| x=functionExpression { $result = x; }
	| x=callExpression     { $result = x; }
	| x=memberExpression   { $result = x; }
	;

newExpression returns [JsExpression result]
@init { List<JsExpression> args = null; }
	: ^( NEW t=leftHandSideExpression ( ^( ARGS { args = new List<JsExpression>(); } (a=expr { args.Add(a); })* ) )? ) { $result = JsExpression.New(t, args); }
	;

functionExpression returns [JsExpression result]
@init { var parms = new List<string>(); }
	: ^( FUNCTION i=identifier? (typeParams = typeParameters)? (parameters = parameterList)? (annotation = typeAnnotation)? (b=block) ) { $result = JsExpression.FunctionDefinition(typeParams, parameters, b, i != null ? i : null, annotation); }
	;

functionDeclaration returns [JsStatement result]
@init { var parms = new List<string>(); }
	: ^( FUNCTIONDECLARATION i=identifier (typeParams = typeParameters)? (parameters = parameterList)? (annotation = typeAnnotation)? (b=block)? ) { $result = new JsFunctionStatement(typeParams, parameters, i, b, annotation); }
	;

callExpression returns [JsExpression result]
@init { var args = new List<JsExpression>(); }
	: ^( CALL ^( NEW t=leftHandSideExpression ) ^( ARGS (a=expr { args.Add(a); })* ) ) { $result = JsExpression.New(t, args); }
	| ^( CALL t=leftHandSideExpression ^( ARGS (a=expr { args.Add(a); })* ) ) { $result = JsExpression.Invocation(t, args); }
	;
	
memberExpression returns [JsExpression result]
	: ^( BYINDEX a=leftHandSideExpression b=expression { $result = JsExpression.Index(a, b); } )
	| ^( BYFIELD a=leftHandSideExpression i=identifier { $result = JsExpression.MemberAccess(a, i); } )
	;

primaryExpression returns [JsExpression result]
	: i=identifier { $result = JsExpression.Identifier(i); }
	| x=literal { $result = x; }
	| ^(PAREXPR x=expression { $result = x; })
	;

literal returns [JsExpression result]
	: THIS { $result = JsExpression.This; }
	| NULL { $result = JsExpression.Null; }
	| x=booleanLiteral { $result = x; }
	| x=numericLiteral { $result = x; }
	| StringLiteral { $result = JsExpression.String(UnescapeStringLiteral($StringLiteral.Text)); }
	| RegularExpressionLiteral { $result = ParseRegexpLiteral($RegularExpressionLiteral.Text); }
	| x=arrayLiteral { $result = x; }
	| x=objectLiteral { $result = x; }
	;

booleanLiteral returns [JsExpression result]
	: TRUE  { $result = JsExpression.True; }
	| FALSE { $result = JsExpression.False; }
	;

numericLiteral returns [JsExpression result]
	: DecimalLiteral      { $result = JsExpression.Number(double.Parse($DecimalLiteral.Text, NumberStyles.Float, CultureInfo.InvariantCulture)); }
	| OctalIntegerLiteral { $result = JsExpression.Number(DecodeOctalNumber($OctalIntegerLiteral.Text)); }
	| HexIntegerLiteral   { $result = JsExpression.Number(int.Parse($HexIntegerLiteral.Text.Substring(2), NumberStyles.HexNumber, CultureInfo.InvariantCulture)); }
	;

arrayLiteral returns [JsExpression result]
@init { var items = new List<JsExpression>(); }
	: ^( ARRAY ( ^( ITEM { x = null; } x=expr? ) { items.Add(x); } )* ) { $result = JsExpression.ArrayLiteral(items); }
	;

objectLiteral returns [JsExpression result]
@init { var props = new List<JsObjectLiteralProperty>(); }
	: ^( OBJECT ( ^( NAMEDVALUE n=propertyName x=expr { props.Add(new JsObjectLiteralProperty(n, x)); } ) )* ) { $result = JsExpression.ObjectLiteral(props); }
	;

propertyName returns [ string result ]
	: i=identifier       { $result = i; }
	| StringLiteral	   { $result = $StringLiteral.Text.Substring(1, $StringLiteral.Text.Length - 2); }
	| x=numericLiteral { $result = ((JsConstantExpression)x).NumberValue.ToString(CultureInfo.InvariantCulture); }
	;

public type returns [TsType result]
    : (( t=typeReference )
    |  ( t=primitiveType )
    |  ( t=objectType )
    |  ( t=constructorType )
    |  ( t=functionType )
    |  ( t=tupleType ))
    (ARRAY_TYPE {t= new TsArrayType(t);})*
	{ $result = t; }
	;

typeReference returns [TsType result]
    : ^( TYPEREF n=identifier args=typeArguments? ) 
    { $result = new TsTypeReference(n, args); }
	;

typeArguments returns [IList<TsType> result]
    @init { var args = new List<TsType>(); }
	: ^( TYPE_ARGS (a=type { args.Add(a); })* )
    { $result = args; }
    ;

primitiveType returns [TsType result]
    : (ANY) {$result = TsPrimitiveType.Any; }
    | (NUMBER) {$result = TsPrimitiveType.Number; }
    | (BOOLEAN) {$result = TsPrimitiveType.Boolean; }
    | (STRING) {$result = TsPrimitiveType.String; }
    | (VOID) {$result = TsPrimitiveType.Void; }
    ;

objectType returns [TsType result]
    @init { var members = new List<TsTypeMember>(); }
    : ^(OBJECT_TYPE (m=typeMember { members.Add(m); })*)
    { $result = new TsObjectType(members); }
    ;

typeMember returns [TsTypeMember result]
    : (( m=propertySignature )
    | (m = callSignature)
    | (m = indexSignature)
    | (m = methodSignature)
    | (m = constructSignature)
    )
    { $result = m; }
    ;

propertySignature returns [TsTypeMember result]
    @init { bool optional = false; }
    : ^(PROP_SIG n=propertyName (QUE {optional = true;})? a=typeAnnotation?)
	{ $result = new TsPropertySignature(n, a, optional); }
	;

typeAnnotation returns [TsType result]
    : ^(COLON t=type)
	{ $result = t; }
    ;

callSignature returns [TsTypeMember result]
    : ^(CALL_SIG (typeParams = typeParameters)? (parameters = parameterList)? (annotation = typeAnnotation)?)
	{ $result = new TsCallSignature(typeParams, parameters, annotation); }
	;

typeParameters returns [IList<TsTypeParameter> result]
    @init { var parameters = new List<TsTypeParameter>(); }
    : ^(TYPE_PARAMS (t= typeParameter {parameters.Add(t);})*)
    { $result = parameters; }
    ;

typeParameter returns [TsTypeParameter result]
    : ^((i = identifier) (c = typeConstraint)?)
    { $result = new TsTypeParameter(i, c);  }
    ;

typeConstraint returns [TsType result]
    : ^(EXTENDS (t=type))
    { $result = t;  }
    ;

parameterList returns [IList<TsParameter> result]
    @init { var parameters = new List<TsParameter>(); }
    : (^(PARAMS (p=parameter {parameters.Add(p);})* (r=restParameter {parameters.Add(r);})?)
    | (r=restParameter {parameters.Add(r);}))
    { $result = parameters;  }
    ;

parameter returns [TsParameter result]
    @init { var optional = false; }
    : ^(PARAM (name=identifier) (modifier = accessibilityModifier)? (QUE {optional = true;})? (a=typeAnnotation)?  (x=expr)?)
    { $result = new TsParameter(name, a, optional, false, modifier);  }
    ;

accessibilityModifier returns [AccessibilityModifier result]
    : (PUBLIC) {$result = AccessibilityModifier.Public; }
    | (PRIVATE) {$result = AccessibilityModifier.Private; }
    | (PROTECTED) {$result = AccessibilityModifier.Protected; }
    ;

restParameter returns [TsParameter result]
    : ^(ELLIPSIS (n=identifier) (a=typeAnnotation)?)
    { $result = new TsParameter(n, a, false, true, null);  }
    ;

indexSignature returns [TsTypeMember result]
    @init { TsPrimitiveType parameterType = null; }
    :^(INDEX_SIG (n=identifier) ((STRING {parameterType = TsPrimitiveType.String;}) | (NUMBER{parameterType = TsPrimitiveType.Number;})) (a=typeAnnotation))
    {$result = new TsIndexSignature(n, parameterType, a);}
    ;

methodSignature returns [TsTypeMember result]
    @init { bool optional = false; }
    :^(METH_SIG p=propertyName (QUE {optional = true;})? (typeParams = typeParameters)? (parameters = parameterList)? (annotation = typeAnnotation)?)
    {$result = new TsMethodSignature(p, optional, typeParams, parameters, annotation);}
    ;

ambientFunctionDeclaration returns [TsSourceElement result]
    @init { bool optional = false; }
    :^(AMBIENTFUNCTION i=identifier (QUE {optional = true;})? (typeParams = typeParameters)? (parameters = parameterList)? (annotation = typeAnnotation)?)
    {$result = new TsAmbientFunctionDeclaration(i, optional, typeParams, parameters, annotation);}
    ;

constructSignature returns [TsTypeMember result]
    :^(CONST_SIG (typeParams = typeParameters)? (parameters = parameterList)? (annotation = typeAnnotation)?)
    {$result = new TsConstructSignature(typeParams, parameters, annotation);}
    ;

functionType returns [TsType result]
    : ^(FUNC_TYPE (typeParams = typeParameters)? (parameters = parameterList)? (t = type)?)
    { $result = new TsFunctionType(typeParams, parameters, t); }
    ;

constructorType returns [TsType result]
    : ^(CONST_TYPE (typeParams = typeParameters)? (parameters = parameterList)? (t = type)?)
    { $result = new TsConstructorType(typeParams, parameters, t); }
    ;

tupleType returns [TsType result]
    @init { List<TsType> types = new List<TsType>(); }
    : ^(TUPLE_TYPE (t = type {types.Add(t);})*)
    { $result = new TsTupleType(types); }
    ;

interfaceElement returns [TsSourceElement result]
    @init { var members = new List<TsTypeMember>(); }
    : ^(INTERFACE i=identifier p=typeParameters? e=extendsClause? (m=typeMember { members.Add(m); })*)
    { $result = new TsInterface(i, p, e, members); }
    ;

extendsClause returns [List<TsTypeReference> result]
    @init { var refs = new List<TsTypeReference>(); }
    : ^(EXTENDS (t = typeReference{refs.Add((TsTypeReference)t);})*)
    { $result = refs; }
    ;

classElement returns [TsSourceElement result]
    @init { var members = new List<TsClassMember>(); }
    : ^(CLASS (i=identifier) (p=typeParameters)? (e=extendsClause)? ((m=classMember) { members.Add(m); })*)
    { $result = new TsClass(i, p, e, members); }
    ;

classMember returns [TsClassMember result]
    : ((m = constructorDeclaration)
    |  (m = methodDeclaration)
    |  (m = classIndexSignature)
    |  (m = getAccessor)
    |  (m = setAccessor)
    |  (m = memberVariableDeclaration))
    { $result = m; }
    ;

constructorDeclaration returns [TsClassMember result]
    @init { var signatures = new List<TsClassConstructorSignature>(); }
    : ^(CONST_DECL (s = constructorCallSignature { signatures.Add(s);})* (f = block))
    { $result = new TsConstructorDeclaration(signatures, f);}
    ;

constructorCallSignature returns [TsClassConstructorSignature result]
    @init { AccessibilityModifier? accessibility = null; }
    : ^(CONST_SIG (a=accessibilityModifier {accessibility = a;})? (parameters = parameterList)?)
    { $result = new TsClassConstructorSignature(accessibility, parameters);}
    ;

methodDeclaration returns [TsClassMember result]
    @init { var signatures = new List<TsClassMethodSignature>(); }
    : ^(METH_DECL (s = methodCallSignature { signatures.Add(s);})* (f = block))
    { $result = new TsMethodDeclaration(signatures, f);}
    ;

methodCallSignature returns [TsClassMethodSignature result]
    @init { AccessibilityModifier? accessibility = null; bool s = false; }
    : ^(METH_SIG (a=accessibilityModifier {accessibility = a;})? (STATIC {s = true;})? (i=identifier) (typeParams = typeParameters)? (parameters = parameterList)? (annotation = typeAnnotation)?)
    { $result = new TsClassMethodSignature(accessibility, s, i, parameters, typeParams, annotation);}
    ;

classIndexSignature returns [TsClassMember result]
    : ((s = indexSignature))
    { $result = new TsClassIndexSignature((TsIndexSignature)s);}
    ;

getAccessor returns [TsClassMember result]
    @init { AccessibilityModifier? accessibility = null; bool s = false; }
    : ^(GET (a=accessibilityModifier {accessibility = a;})? (STATIC {s = true;})? (i=identifier) (annotation = typeAnnotation)? (f = block))
    { $result = new TsClassGetAccessor(accessibility, s, i, annotation, f); }
    ;

setAccessor returns [TsClassMember result]
    @init { AccessibilityModifier? accessibility = null; bool s = false; }
    : ^(SET (a=accessibilityModifier {accessibility = a;})? (STATIC {s = true;})? (i=identifier) (p = parameter) (annotation = typeAnnotation)? (f = block))
    { $result = new TsClassSetAccessor(accessibility, s, i, p, annotation, f); }
    ;

moduleElement returns [TsSourceElement result]
    @init { var elements = new List<TsSourceElement>(); }
    : ^(MODULE (i=StringLiteral) (e = sourceElement {elements.Add(e);})*)
    { $result = new TsModule($StringLiteral.Text.Substring(1, $StringLiteral.Text.Length - 2), elements); }
    ;

exportDeclaration returns [TsSourceElement result]
    : ^(EXPORT  (e = sourceElement))
    { $result = new TsExportElement(e); }
    ;

ambientDeclaration returns [TsSourceElement result]
    : ^(DECLARE  (e = sourceElement))
    { $result = new TsAmbientDeclaration(e); }
    ;

identifier returns [string result]
    : (x= ANY
    | x= CONSTRUCTOR
    | x= DECLARE
    | x= GET
    | x= SET
    | x= MODULE
    | x= REQUIRE
    | x= NUMBER
    | x= STRING
    | x= TYPE
    | x= FINAL
    | x= THROWS
    | x= Identifier)
    { $result = x.Text; }
    ;

importDeclaration returns [TsSourceElement result]
    : ^(IMPORT a = identifier m = StringLiteral)
    { $result = new TsImportDeclaration(a,m.Text.Substring(1, m.Text.Length - 2)); }
    ;

memberVariableDeclaration returns [TsClassMember result]
    @init { AccessibilityModifier? accessibility = null; bool s = false; }
    : ^( VAR (a=accessibilityModifier {accessibility = a;})? (STATIC { s=true;})? i=identifier ann=typeAnnotation? (ASSIGN e=expression)?)
    { $result = new TsClassMemberDeclaration(accessibility,s,new JsVariableDeclaration(i, e, ann)); }
    ;