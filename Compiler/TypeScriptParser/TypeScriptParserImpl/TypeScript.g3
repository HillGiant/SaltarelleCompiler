grammar TypeScript;

options
{
	output = AST;
	language = CSharp3;
	k=3;
}

tokens {

// Reserved words
    NULL		= 'null' ;
    TRUE		= 'true' ;
    FALSE		= 'false' ;

// Keywords
    BREAK		= 'break' ;
    CASE		= 'case' ;
    CATCH 		= 'catch' ;
    CONTINUE 	        = 'continue' ;
    DEFAULT		= 'default' ;
    DELETE		= 'delete' ;
    DO 		        = 'do' ;
    ELSE 		= 'else' ;
    FINALLY 	        = 'finally' ;
    FOR 		= 'for' ;
    FUNCTION 	        = 'function' ;
    IF 		        = 'if' ;
    IN 		        = 'in' ;
    INSTANCEOF 	        = 'instanceof' ;
    NEW 		= 'new' ;
    RETURN 		= 'return' ;
    SWITCH 		= 'switch' ;
    THIS 		= 'this' ;
    THROW 		= 'throw' ;
    TRY 		= 'try' ;
    TYPE 		= 'type' ;
    TYPEOF 		= 'typeof' ;
    VAR 		= 'var' ;
    VOID 		= 'void' ;
    WHILE 		= 'while' ;
    WITH 		= 'with' ;

// Typescript reserved words
    ABSTRACT	        = 'abstract' ;
    CLASS 		= 'class' ;
    DECLARE   	        = 'declare';
    INTERFACE 	        = 'interface' ;
    EXTENDS   	        = 'extends';
    MODULE    	        = 'module';
    EXPORT    	        = 'export';
    IMPORT    	        = 'import';
    PRIVATE 	        = 'private' ;
    PROTECTED 	        = 'protected' ;
    PUBLIC		= 'public' ;
    CONSTRUCTOR 	= 'constructor' ;
    GET 		= 'get' ;
    SET 		= 'set' ;
    REQUIRE		= 'require';

// Typescript predefined types
    ANY		        = 'any' ;
    NUMBER		= 'number' ;
    BOOLEAN		= 'boolean' ;
    STRING		= 'string' ;
    VOID		= 'void' ;
        
// Future reserved words
    BOOLEAN 	        = 'boolean' ;
    BYTE 		= 'byte' ;
    CHAR 		= 'char' ;
    CONST 		= 'const' ;
    DOUBLE		= 'double' ;
    ENUM 		= 'enum' ;
    FINAL 		= 'final' ;
    FLOAT 		= 'float' ;
    GOTO 		= 'goto' ;
    IMPLEMENTS 	        = 'implements' ;
    INT 		= 'int' ;
    LONG 		= 'long' ;
    NATIVE 		= 'native' ;
    PACKAGE 	        = 'package' ;
    SHORT 		= 'short' ;
    STATIC 		= 'static' ;
    SUPER 		= 'super' ;
    SYNCHRONIZED 	= 'synchronized' ;
    THROWS 		= 'throws' ;
    TRANSIENT 	        = 'transient' ;
    VOLATILE 	        = 'volatile' ;

// Made-up keywords
    YIELD		= 'yield';
    AWAIT               = 'await';

// Punctuators
    LBRACE		= '{' ;
    RBRACE		= '}' ;
    LPAREN		= '(' ;
    RPAREN		= ')' ;
    LBRACK		= '[' ;
    RBRACK		= ']' ;
    DOT		        = '.' ;
    SEMIC		= ';' ;
    COMMA		= ',' ;
    LT	                = '<' ;
    GT		        = '>' ;
    LTE		        = '<=' ;
    GTE		        = '>=' ;
    EQ		        = '==' ;
    NEQ		        = '!=' ;
    SAME		= '===' ;
    NSAME		= '!==' ;
    ADD		        = '+' ;
    SUB		        = '-' ;
    MUL		        = '*' ;
    MOD		        = '%' ;
    INC		        = '++' ;
    DEC		        = '--' ;
    SHL		        = '<<' ;
    SHR		        = '>>' ;
    SHU		        = '>>>' ;
    AND		        = '&' ;
    OR		        = '|' ;
    XOR		        = '^' ;
    NOT		        = '!' ;
    INV		        = '~' ;
    LAND		= '&&' ;
    LOR		        = '||' ;
    QUE		        = '?' ;
    COLON		= ':' ;
    ASSIGN		= '=' ;
    ADDASS		= '+=' ;
    SUBASS		= '-=' ;
    MULASS		= '*=' ;
    MODASS		= '%=' ;
    SHLASS		= '<<=' ;
    SHRASS		= '>>=' ;
    SHUASS		= '>>>=' ;
    ANDASS		= '&=' ;
    ORASS		= '|=' ;
    XORASS		= '^=' ;
    DIV		        = '/' ;
    DIVASS		= '/=' ;
    ELLIPSIS            = '...';
    ARROW               = '=>';

    ARRAY_TYPE;
    FUNCTION_TYPE;
    PARAM;
    PARAM_ARRAY;
    PARAM_OPT;
    PARAMS;
    VAR_OPT;
    CONSTRUCTOR;
    INDEX;
    OBJECT_TYPE;
    ARGS ;
    ARRAY ;
    BLOCK ;
    BYFIELD ;
    BYINDEX ;
    CALL ;
    CEXPR ;
    EXPR ;
    FORITER ;
    FORSTEP ;
    ITEM ;
    LABELLED ;
    NAMEDVALUE ;
    NEG ;
    OBJECT ;
    PAREXPR ;
    PDEC ;
    PINC ;
    POS ;
    TYPE_PARAMS;
    TYPE_ARGS;
    FUNCTIONDECLARATION ;
    TYPEREF;
    PROP_SIG;
    CALL_SIG;
    CONST_SIG;
    INDEX_SIG;
    METH_SIG;
    FUNC_TYPE;
    CONST_TYPE;
    TUPLE_TYPE;
}

@lexer::namespace { TypeScriptParser.TypeScriptParserImpl }

@lexer::header { #pragma warning disable 219, 162, 3021 }

@parser::namespace { TypeScriptParser.TypeScriptParserImpl }

@parser::header { #pragma warning disable 219, 162, 3021 }

@lexer::members {
	public override void ReportError(RecognitionException e) { ErrorReporter.ReportError(e.Line, e.CharPositionInLine, GetErrorMessage(e, TokenNames)); }
	public IErrorReporter ErrorReporter { get; set; }
}

@parser::members {
	public override void ReportError(RecognitionException e) { ErrorReporter.ReportError(e.Line, e.CharPositionInLine, GetErrorMessage(e, TokenNames)); }
	public IErrorReporter ErrorReporter { get; set; }
}

//
// $<	A.1 Lexical Grammar
//

// Added for lexing purposes

fragment BSLASH
	: '\\'
	;
	
fragment DQUOTE
	: '"'
	;
	
fragment SQUOTE
	: '\''
	;

// $<	Whitespace (7.2)

fragment TAB
	: '\u0009'
	;

fragment VT // Vertical TAB
	: '\u000b'
	;

fragment FF // Form Feed
	: '\u000c'
	;

fragment SP // Space
	: '\u0020'
	;

fragment NBSP // Non-Breaking Space
	: '\u00a0'
	;

fragment USP // Unicode Space Separator (rest of Unicode category Zs)
	: '\u1680'  // OGHAM SPACE MARK
	| '\u180E'  // MONGOLIAN VOWEL SEPARATOR
	| '\u2000'  // EN QUAD
	| '\u2001'  // EM QUAD
	| '\u2002'  // EN SPACE
	| '\u2003'  // EM SPACE
	| '\u2004'  // THREE-PER-EM SPACE
	| '\u2005'  // FOUR-PER-EM SPACE
	| '\u2006'  // SIX-PER-EM SPACE
	| '\u2007'  // FIGURE SPACE
	| '\u2008'  // PUNCTUATION SPACE
	| '\u2009'  // THIN SPACE
	| '\u200A'  // HAIR SPACE
	| '\u202F'  // NARROW NO-BREAK SPACE
	| '\u205F'  // MEDIUM MATHEMATICAL SPACE
	| '\u3000'  // IDEOGRAPHIC SPACE
	;

WhiteSpace
	: ( TAB | VT | FF | SP | NBSP | USP )+ { $channel = Hidden; }
	;

// $>


// $<	Line terminators (7.3)

fragment LF // Line Feed
	: '\n'
	;

fragment CR // Carriage Return
	: '\r'
	;

fragment LS // Line Separator
	: '\u2028'
	;

fragment PS // Paragraph Separator
	: '\u2029'
	;

fragment LineTerminator
	: CR | LF | LS | PS
	;
		
EOL
	: ( ( CR LF? ) | LF | LS | PS ) { $channel = Hidden; }
	;
// $>

// $<	Comments (7.4)

MultiLineComment
	: '/*' ( options { greedy = false; } : . )* '*/' { $channel = Hidden; }
	;

SingleLineComment
	: '//' ( ~( LineTerminator ) )* { $channel = Hidden; }
	;

// $>

// $<	Tokens (7.5)

token
	: reservedWord
	| Identifier
	| punctuator
	| numericLiteral
	| StringLiteral
	;

// $<	Reserved words (7.5.1)

reservedWord
	: keyword
	| futureReservedWord
	| NULL
	| booleanLiteral
	;

// $>
	
// $<	Keywords (7.5.2)

keyword
	: BREAK
	| CASE
	| CATCH
	| CONTINUE
	| DEFAULT
	| DELETE
	| DO
	| ELSE
	| FINALLY
	| FOR
	| FUNCTION
	| IF
	| IN
	| INSTANCEOF
	| NEW
	| RETURN
	| SWITCH
	| THIS
	| THROW
	| TRY
	| TYPEOF
	| VAR
	| VOID
	| WHILE
	| WITH
	;

// $>

// $<	Future reserved words (7.5.3)

futureReservedWord
	: ABSTRACT
	| BOOLEAN
	| BYTE
	| CHAR
	| CLASS
	| CONST
//	| DEBUGGER
	| DOUBLE
	| ENUM
	| EXPORT
	| EXTENDS
	| FINAL
	| FLOAT
	| GOTO
	| IMPLEMENTS
	| IMPORT
	| INT
	| INTERFACE
	| LONG
	| NATIVE
	| PACKAGE
	| PRIVATE
	| PROTECTED
	| PUBLIC
	| SHORT
	| STATIC
	| SUPER
	| SYNCHRONIZED
	| THROWS
	| TRANSIENT
	| VOLATILE
	;

// $>

// $>


// $<	Identifiers (7.6)

fragment IdentifierStartASCII
	: 'a'..'z' | 'A'..'Z'
	| '$'
	| '_'
	| BSLASH 'u' HexDigit HexDigit HexDigit HexDigit // UnicodeEscapeSequence
	;

/*
The first two alternatives define how ANTLR can match ASCII characters which can be considered as part of an identifier.
The last alternative matches other characters in the unicode range that can be sonsidered as part of an identifier.
*/
fragment IdentifierPart
	: DecimalDigit
	| IdentifierStartASCII
	| { IsIdentifierPartUnicode(input.LA(1)) }? { MatchAny(); }
	;

fragment IdentifierNameASCIIStart
	: IdentifierStartASCII IdentifierPart*
	;

/*
The second alternative acts as an action driven fallback to evaluate other characters in the unicode range than the ones in the ASCII subset.
Due to the first alternative this grammar defines enough so that ANTLR can generate a lexer that correctly predicts identifiers with characters in the ASCII range.
In that way keywords, other reserved words and ASCII identifiers are recognized with standard ANTLR driven logic. When the first character for an identifier fails to 
match this ASCII definition, the lexer calls ConsumeIdentifierUnicodeStart because of the action in the alternative. This method checks whether the character matches 
as first character in ranges other than ASCII and consumes further characters belonging to the identifier with help of mIdentifierPart generated out of the 
IdentifierPart rule above.
*/
Identifier
	: IdentifierNameASCIIStart
	| { ConsumeIdentifierUnicodeStart(); }
	;

// $>

// $<	Punctuators (7.7)

punctuator
	: LBRACE
	| RBRACE
	| LPAREN
	| RPAREN
	| LBRACK
	| RBRACK
	| DOT
	| SEMIC
	| COMMA
	| LT
	| GT
	| LTE
	| GTE
	| EQ
	| NEQ
	| SAME
	| NSAME
	| ADD
	| SUB
	| MUL
	| MOD
	| INC
	| DEC
	| SHL
	| SHR
	| SHU
	| AND
	| OR
	| XOR
	| NOT
	| INV
	| LAND
	| LOR
	| QUE
	| COLON
	| ASSIGN
	| ADDASS
	| SUBASS
	| MULASS
	| MODASS
	| SHLASS
	| SHRASS
	| SHUASS
	| ANDASS
	| ORASS
	| XORASS
	| DIV
	| DIVASS
	;

// $>

// $<	Literals (7.8)

literal
	: NULL
	| booleanLiteral
	| numericLiteral
	| StringLiteral
	| RegularExpressionLiteral
	;

booleanLiteral
	: TRUE
	| FALSE
	;

// $<	Numeric literals (7.8.3)

/*
Note: octal literals are described in the B Compatibility section.
These are removed from the standards but are here for backwards compatibility with earlier ECMAScript definitions.
*/

fragment DecimalDigit
	: '0'..'9'
	;

fragment HexDigit
	: DecimalDigit | 'a'..'f' | 'A'..'F'
	;

fragment OctalDigit
	: '0'..'7'
	;

fragment ExponentPart
	: ( 'e' | 'E' ) ( '+' | '-' )? DecimalDigit+
	;

fragment DecimalIntegerLiteral
	: '0'
	| '1'..'9' DecimalDigit*
	;

DecimalLiteral
	: DecimalIntegerLiteral '.' DecimalDigit* ExponentPart?
	| '.' DecimalDigit+ ExponentPart?
	| DecimalIntegerLiteral ExponentPart?
	;

OctalIntegerLiteral
	: '0' OctalDigit+
	;

HexIntegerLiteral
	: ( '0x' | '0X' ) HexDigit+
	;

numericLiteral
	: DecimalLiteral
	| OctalIntegerLiteral
	| HexIntegerLiteral
	;

// $>

// $<	String literals (7.8.4)

/*
Note: octal escape sequences are described in the B Compatibility section.
These are removed from the standards but are here for backwards compatibility with earlier ECMAScript definitions.
*/
	
fragment CharacterEscapeSequence
	: ~( DecimalDigit | 'x' | 'u' | LineTerminator ) // Concatenation of SingleEscapeCharacter and NonEscapeCharacter
	;

fragment ZeroToThree
	: '0'..'3'
	;
	
fragment OctalEscapeSequence
	: OctalDigit
	| ZeroToThree OctalDigit
	| '4'..'7' OctalDigit
	| ZeroToThree OctalDigit OctalDigit
	;
	
fragment HexEscapeSequence
	: 'x' HexDigit HexDigit
	;
	
fragment UnicodeEscapeSequence
	: 'u' HexDigit HexDigit HexDigit HexDigit
	;

fragment EscapeSequence
	:
	BSLASH 
	(
		CharacterEscapeSequence 
		| OctalEscapeSequence
		| HexEscapeSequence
		| UnicodeEscapeSequence
	)
	;

StringLiteral
	: SQUOTE ( ~( SQUOTE | BSLASH | LineTerminator ) | EscapeSequence )* SQUOTE
	| DQUOTE ( ~( DQUOTE | BSLASH | LineTerminator ) | EscapeSequence )* DQUOTE
	;

// $>

// $<	Regular expression literals (7.8.5)

fragment BackslashSequence
	: BSLASH ~( LineTerminator )
	;

fragment RegularExpressionFirstChar
	: ~ ( LineTerminator | MUL | BSLASH | DIV )
	| BackslashSequence
	;

fragment RegularExpressionChar
	: ~ ( LineTerminator | BSLASH | DIV )
	| BackslashSequence
	;

RegularExpressionLiteral
	: { AreRegularExpressionsEnabled }?=> DIV RegularExpressionFirstChar RegularExpressionChar* DIV IdentifierPart*
	;

// $>

// $>

// $>

//
// $<	A.3 Expressions (11)
//

// $<Primary expressions (11.1)

primaryExpression
	: THIS
	| Identifier
	| literal
	| arrayLiteral
	| objectLiteral
	| lpar=LPAREN expression RPAREN -> ^( PAREXPR[$lpar, "PAREXPR"] expression )
	;

arrayLiteral
	: lb=LBRACK ( arrayItem ( COMMA arrayItem )* )? RBRACK
	-> ^( ARRAY[$lb, "ARRAY"] arrayItem* )
	;

arrayItem
	: ( expr=assignmentExpression | { input.LA(1) == COMMA }? )
	-> ^( ITEM $expr? )
	;

objectLiteral
	: lb=LBRACE ( nameValuePair ( COMMA nameValuePair )* )? RBRACE
	-> ^( OBJECT[$lb, "OBJECT"] nameValuePair* )
	;
	
nameValuePair
	: propertyName COLON assignmentExpression
	-> ^( NAMEDVALUE propertyName assignmentExpression )
	;

propertyName
	: Identifier
	| StringLiteral
	| numericLiteral
	;

// $>

// $<Left-hand-side expressions (11.2)

/*
Refactored some rules to make them LL(*) compliant:
all the expressions surrounding member selection and calls have been moved to leftHandSideExpression to make them right recursive
*/

memberExpression
	: primaryExpression
	| functionExpression
	| newExpression
	;

newExpression
	: NEW^ primaryExpression
	;

	
arguments
	: LPAREN ( assignmentExpression ( COMMA assignmentExpression )* )? RPAREN
	-> ^( ARGS assignmentExpression* )
	;
	
leftHandSideExpression
	:
	(
		memberExpression 		-> memberExpression
	)
	(
		arguments			-> ^( CALL $leftHandSideExpression arguments )
		| LBRACK expression RBRACK	-> ^( BYINDEX $leftHandSideExpression expression )
		| DOT Identifier		-> ^( BYFIELD $leftHandSideExpression Identifier )
	)*
	;

// $>

// $<Postfix expressions (11.3)

/*
The specification states that there are no line terminators allowed before the postfix operators.
This is enforced by the call to PromoteEOL in the action before ( INC | DEC ).
We only must promote EOLs when the la is INC or DEC because this production is chained as all expression rules.
In other words: only promote EOL when we are really in a postfix expression. A check on the la will ensure this.
*/
postfixExpression
	: leftHandSideExpression { if (input.LA(1) == INC || input.LA(1) == DEC) PromoteEOL(null); } ( postfixOperator^ )?
	;
	
postfixOperator
	: op=INC { $op.Type = PINC; }
	| op=DEC { $op.Type = PDEC; }
	;

// $>

// $<Unary operators (11.4)

unaryExpression
	: postfixExpression
	| unaryOperator^ unaryExpression
	;
	
unaryOperator
	: DELETE
	| VOID
	| TYPEOF
	| INC
	| DEC
	| op=ADD { $op.Type = POS; }
	| op=SUB { $op.Type = NEG; }
	| INV
	| NOT
	;

// $>

// $<Multiplicative operators (11.5)

multiplicativeExpression
	: unaryExpression ( ( MUL | DIV | MOD )^ unaryExpression )*
	;

// $>

// $<Additive operators (11.6)

additiveExpression
	: multiplicativeExpression ( ( ADD | SUB )^ multiplicativeExpression )*
	;

// $>
	
// $<Bitwise shift operators (11.7)

shiftExpression
	: additiveExpression ( ( SHL | SHR | SHU )^ additiveExpression )*
	;

// $>
	
// $<Relational operators (11.8)

relationalExpression
	: shiftExpression ( ( LT | GT | LTE | GTE | INSTANCEOF | IN )^ shiftExpression )*
	;

relationalExpressionNoIn
	: shiftExpression ( ( LT | GT | LTE | GTE | INSTANCEOF )^ shiftExpression )*
	;

// $>
	
// $<Equality operators (11.9)

equalityExpression
	: relationalExpression ( ( EQ | NEQ | SAME | NSAME )^ relationalExpression )*
	;

equalityExpressionNoIn
	: relationalExpressionNoIn ( ( EQ | NEQ | SAME | NSAME )^ relationalExpressionNoIn )*
	;

// $>
		
// $<Binary bitwise operators (11.10)

bitwiseANDExpression
	: equalityExpression ( AND^ equalityExpression )*
	;

bitwiseANDExpressionNoIn
	: equalityExpressionNoIn ( AND^ equalityExpressionNoIn )*
	;
		
bitwiseXORExpression
	: bitwiseANDExpression ( XOR^ bitwiseANDExpression )*
	;
		
bitwiseXORExpressionNoIn
	: bitwiseANDExpressionNoIn ( XOR^ bitwiseANDExpressionNoIn )*
	;
	
bitwiseORExpression
	: bitwiseXORExpression ( OR^ bitwiseXORExpression )*
	;
	
bitwiseORExpressionNoIn
	: bitwiseXORExpressionNoIn ( OR^ bitwiseXORExpressionNoIn )*
	;

// $>
	
// $<Binary logical operators (11.11)

logicalANDExpression
	: bitwiseORExpression ( LAND^ bitwiseORExpression )*
	;

logicalANDExpressionNoIn
	: bitwiseORExpressionNoIn ( LAND^ bitwiseORExpressionNoIn )*
	;
	
logicalORExpression
	: logicalANDExpression ( LOR^ logicalANDExpression )*
	;
	
logicalORExpressionNoIn
	: logicalANDExpressionNoIn ( LOR^ logicalANDExpressionNoIn )*
	;

// $>
	
// $<Conditional operator (11.12)

conditionalExpression
	: logicalORExpression ( QUE^ assignmentExpression COLON! assignmentExpression )?
	;

conditionalExpressionNoIn
	: logicalORExpressionNoIn ( QUE^ assignmentExpressionNoIn COLON! assignmentExpressionNoIn )?
	;
	
// $>

// $<Assignment operators (11.13)

/*
The specification defines the AssignmentExpression rule as follows:
AssignmentExpression :
	ConditionalExpression 
	LeftHandSideExpression AssignmentOperator AssignmentExpression
This rule has a LL(*) conflict. Resolving this with a syntactical predicate will yield something like this:

assignmentExpression
	: ( leftHandSideExpression assignmentOperator )=> leftHandSideExpression assignmentOperator^ assignmentExpression
	| conditionalExpression
	;
assignmentOperator
	: ASSIGN | MULASS | DIVASS | MODASS | ADDASS | SUBASS | SHLASS | SHRASS | SHUASS | ANDASS | XORASS | ORASS
	;
	
But that didn't seem to work. Terence Par writes in his book that LL(*) conflicts in general can best be solved with auto backtracking. But that would be 
a performance killer for such a heavy used rule.
The solution I came up with is to always invoke the conditionalExpression first and than decide what to do based on the result of that rule.
When the rule results in a Tree that can't be coming from a left hand side expression, then we're done.
When it results in a Tree that is coming from a left hand side expression and the LA(1) is an assignment operator then parse the assignment operator
followed by the right recursive call.
*/
assignmentExpression
@init
{
	bool? isLhs = null;
}
	: lhs=conditionalExpression
	( { IsLeftHandSideAssign(lhs, ref isLhs) }? assignmentOperator^ assignmentExpression )?	
	;

assignmentOperator
	: ASSIGN | MULASS | DIVASS | MODASS | ADDASS | SUBASS | SHLASS | SHRASS | SHUASS | ANDASS | XORASS | ORASS
	;

assignmentExpressionNoIn
@init
{
	bool? isLhs = null;
}
	: lhs=conditionalExpressionNoIn
	( { IsLeftHandSideAssign(lhs, ref isLhs) }? assignmentOperator^ assignmentExpressionNoIn )?
	;
	
// $>
	
// $<Comma operator (11.14)

public expression
	: exprs+=assignmentExpression ( COMMA exprs+=assignmentExpression )*
	-> { $exprs.Count > 1 }? ^( CEXPR $exprs+ )
	-> $exprs
	;

expressionNoIn
	: exprs+=assignmentExpressionNoIn ( COMMA exprs+=assignmentExpressionNoIn )*
	-> { $exprs.Count > 1 }? ^( CEXPR $exprs+ )
	-> $exprs
	;

// $>

// $>
	
//
// $<	A.4 Statements (12)
//

/*
This rule handles semicolons reported by the lexer and situations where the ECMA 3 specification states there should be semicolons automaticly inserted.
The auto semicolons are not actually inserted but this rule behaves as if they were.

In the following situations an ECMA 3 parser should auto insert absent but grammaticly required semicolons:
- the current token is a right brace
- the current token is the end of file (EOF) token
- there is at least one end of line (EOL) token between the current token and the previous token.

The RBRACE is handled by matching it but not consuming it.
The EOF needs no further handling because it is not consumed by default.
The EOL situation is handled by promoting the EOL or MultiLineComment with an EOL present from off channel to on channel
and thus making it parseable instead of handling it as white space. This promoting is done in the action PromoteEOL.
*/
semic
@init
{
	// Mark current position so we can unconsume a RBRACE.
	int marker = input.Mark();
	// Promote EOL if appropriate	
	PromoteEOL(retval);
}
	: SEMIC
	| EOF
	| RBRACE { input.Rewind(marker); }
	| EOL | MultiLineComment // (with EOL in it)
	;

/*
To solve the ambiguity between block and objectLiteral via expressionStatement all but the block alternatives have been moved to statementTail.
Now when k = 1 and a semantical predicate is defined ANTLR generates code that always will prefer block when the LA(1) is a LBRACE.
This will result in the same behaviour that is described in the specification under 12.4 on the expressionStatement rule.
*/
statement
options
{
	k = 1 ;
}
	: { input.LA(1) == LBRACE }? block
	| statementTail
	;
	
statementTail
	: variableStatement
	| emptyStatement
	| expressionStatement
	| ifStatement
	| iterationStatement
	| continueStatement
	| breakStatement
	| returnStatement
	| withStatement
	| labelledStatement
	| switchStatement
	| throwStatement
	| tryStatement
	;

// $<Block (12.1)

block
	: lb=LBRACE statement* RBRACE
	-> ^( BLOCK[$lb, "BLOCK"] statement* )
	;

// $>
	
// $<Variable statement 12.2)

variableStatement
	: VAR variableDeclaration ( COMMA variableDeclaration )* semic
	-> ^( VAR variableDeclaration+ )
	;

variableDeclaration
	: Identifier ( ASSIGN^ assignmentExpression )?
	;
	
variableDeclarationNoIn
	: Identifier ( ASSIGN^ assignmentExpressionNoIn )?
	;

// $>
	
// $<Empty statement (12.3)

emptyStatement
	: SEMIC
	;

// $>
	
// $<Expression statement (12.4)

/*
The look ahead check on LBRACE and FUNCTION the specification mentions has been left out and its function, resolving the ambiguity between:
- functionExpression and functionDeclaration
- block and objectLiteral
are moved to the statement and sourceElement rules.
*/
expressionStatement
	: expression semic!
	;

// $>
	
// $<The if statement (12.5)

ifStatement
// The predicate is there just to get rid of the warning. ANTLR will handle the dangling else just fine.
	: IF LPAREN expression RPAREN statement ( { input.LA(1) == ELSE }? ELSE statement )?
	-> ^( IF expression statement+ )
	;

// $>
	
// $<Iteration statements (12.6)

iterationStatement
	: doStatement
	| whileStatement
	| forStatement
	;
	
doStatement
	: DO statement WHILE LPAREN expression RPAREN semic
	-> ^( DO statement expression )
	;
	
whileStatement
	: WHILE^ LPAREN! expression RPAREN! statement
	;

/*
The forStatement production is refactored considerably as the specification contains a very none LL(*) compliant definition.
The initial version was like this:	

forStatement
	: FOR^ LPAREN! forControl RPAREN! statement
	;
forControl
options
{
	backtrack = true ;
	//k = 3 ;
}
	: stepClause
	| iterationClause
	;
stepClause
options
{
	memoize = true ;
}
	: ( ex1=expressionNoIn | var=VAR variableDeclarationNoIn ( COMMA variableDeclarationNoIn )* )? SEMIC ex2=expression? SEMIC ex3=expression?
	-> { $var != null }? ^( FORSTEP ^( VAR[$var] variableDeclarationNoIn+ ) ^( EXPR $ex2? ) ^( EXPR $ex3? ) )
	-> ^( FORSTEP ^( EXPR $ex1? ) ^( EXPR $ex2? ) ^( EXPR $ex3? ) )
	;
iterationClause
options
{
	memoize = true ;
}
	: ( leftHandSideExpression | var=VAR variableDeclarationNoIn ) IN expression
	-> { $var != null }? ^( FORITER ^( VAR[$var] variableDeclarationNoIn ) ^( EXPR expression ) )
	-> ^( FORITER ^( EXPR leftHandSideExpression ) ^( EXPR expression ) )
	;
	
But this completely relies on the backtrack feature and capabilities of ANTLR. 
Furthermore backtracking seemed to have 3 major drawbacks:
- the performance cost of backtracking is considerably
- didn't seem to work well with ANTLRWorks
- when introducing a k value to optimize the backtracking away, ANTLR runs out of heap space
*/
forStatement
	: FOR^ LPAREN! forControl RPAREN! statement
	;

forControl
	: forControlVar
	| forControlExpression
	| forControlSemic
	;

forControlVar
	: VAR variableDeclarationNoIn
	(
		(
			IN expression
			-> ^( FORITER ^( VAR variableDeclarationNoIn ) ^( EXPR expression ) )
		)
		|
		(
			( COMMA variableDeclarationNoIn )* SEMIC ex1=expression? SEMIC ex2=expression?
			-> ^( FORSTEP ^( VAR variableDeclarationNoIn+ ) ^( EXPR $ex1? ) ^( EXPR $ex2? ) )
		)
	)
	;

forControlExpression
@init
{
	bool? isLhs = null;
}
	: ex1=expressionNoIn
	( 
		{ IsLeftHandSideIn(ex1, ref isLhs) }? (
			IN ex2=expression
			-> ^( FORITER ^( EXPR $ex1 ) ^( EXPR $ex2 ) )
		)
		|
		(
			SEMIC ex2=expression? SEMIC ex3=expression?
			-> ^( FORSTEP ^( EXPR $ex1 ) ^( EXPR $ex2? ) ^( EXPR $ex3? ) )
		)
	)
	;

forControlSemic
	: SEMIC ex1=expression? SEMIC ex2=expression?
	-> ^( FORSTEP ^( EXPR ) ^( EXPR $ex1? ) ^( EXPR $ex2? ) )
	;

// $>
	
// $<The continue statement (12.7)

/*
The action with the call to PromoteEOL after CONTINUE is to enforce the semicolon insertion rule of the specification that there are
no line terminators allowed beween CONTINUE and the optional identifier.
As an optimization we check the la first to decide whether there is an identier following.
*/
continueStatement
	: CONTINUE^ { if (input.LA(1) == Identifier) PromoteEOL(null); } Identifier? semic!
	;

// $>
	
// $<The break statement (12.8)

/*
The action with the call to PromoteEOL after BREAK is to enforce the semicolon insertion rule of the specification that there are
no line terminators allowed beween BREAK and the optional identifier.
As an optimization we check the la first to decide whether there is an identier following.
*/
breakStatement
	: BREAK^ { if (input.LA(1) == Identifier) PromoteEOL(null); } Identifier? semic!
	;

// $>
	
// $<The return statement (12.9)

/*
The action calling PromoteEOL after RETURN ensures that there are no line terminators between RETURN and the optional expression as the specification states.
When there are these get promoted to on channel and thus virtual semicolon wannabees.
So the folowing code:

return
1

will be parsed as:

return;
1;
*/
returnStatement
	: RETURN^ { PromoteEOL(null); } expression? semic!
	;

// $>
	
// $<The with statement (12.10)

withStatement
	: WITH^ LPAREN! expression RPAREN! statement
	;

// $>
	
// $<The switch statement (12.11)

switchStatement
@init
{
	int defaultClauseCount = 0;
}
	: SWITCH LPAREN expression RPAREN LBRACE ( { defaultClauseCount == 0 }?=> c+=defaultClause { defaultClauseCount++; } | c+=caseClause )* RBRACE
	-> ^( SWITCH expression $c* )
	;

caseClause
	: CASE^ expression COLON! statement*
	;
	
defaultClause
	: DEFAULT^ COLON! statement*
	;

// $>
	
// $<Labelled statements (12.12)

labelledStatement
	: Identifier COLON statement
	-> ^( LABELLED Identifier statement )
	;

// $>
	
// $<The throw statement (12.13)

/*
The action calling PromoteEOL after THROW ensures that there are no line terminators between THROW and the expression as the specification states.
When there are line terminators these get promoted to on channel and thus to virtual semicolon wannabees.
So the folowing code:

throw
new Error()

will be parsed as:

throw;
new Error();

which will yield a recognition exception!
*/
throwStatement
	: THROW^ { PromoteEOL(null); } expression semic!
	;

// $>
	
// $<The try statement (12.14)

tryStatement
	: TRY^ block ( catchClause finallyClause? | finallyClause )
	;
	
catchClause
	: CATCH^ LPAREN! Identifier RPAREN! block
	;
	
finallyClause
	: FINALLY^ block
	;

// $>

// $>

//
// $<	A.5 Functions and Programs (13, 14)
//

// $<	Function Definition (13)

functionDeclaration
	: FUNCTION name=Identifier formalParameterList functionBody
	-> ^( FUNCTIONDECLARATION $name formalParameterList functionBody )
	;

functionExpression
	: FUNCTION name=Identifier? formalParameterList functionBody
	-> ^( FUNCTION $name? formalParameterList functionBody )
	;

formalParameterList
	: LPAREN ( Identifier ( COMMA Identifier )* )? RPAREN
	-> ^( ARGS Identifier* )
	;

functionBody
	: lb=LBRACE sourceElement* RBRACE
	-> ^( BLOCK[$lb, "BLOCK"] sourceElement* )
	;

// $>
	
// $<	Program (14)

public program
	: sourceElement*
	;

/*
By setting k  to 1 for this rule and adding the semantical predicate ANTRL will generate code that will always prefer functionDeclararion over functionExpression
here and therefor remove the ambiguity between these to production.
This will result in the same behaviour that is described in the specification under 12.4 on the expressionStatement rule.
*/
public sourceElement
options
{
	k = 1 ;
}
	: { input.LA(1) == FUNCTION }? functionDeclaration
	| statement
    | interfaceDeclaration
    | classDeclaration
    | moduleDeclaration
    | importDeclaration
    | enumDeclaration
	;

// $>

// $>

// <$ Typescript extensions

// $<	Type Parameters (3.5.1)

typeConstraint
    : EXTENDS type -> ^(EXTENDS type)
    ;

typeParameters
    : LT typeParameter (COMMA typeParameter)* GT-> ^(TYPE_PARAMS typeParameter*)
    ;

typeParameter
    : Identifier typeConstraint? -> ^(Identifier typeConstraint?)
    ;
    
// $>

// $<	Type Arguments (3.5.2)

typeArguments
    : LT type (COMMA type)* GT-> ^(TYPE_ARGS type*)
    ;


// $>

// $<	Types (3.7)

public type
options
{
	k = 4 ;
}
    : primaryType
    //| unionType
    | functionType
    | constructorType
    ;
    
nonTupleType
options
{
	k = 4 ;
	backtrack = true;
}
    : predefinedType
    | typeReference
    | objectType
    | typeQuery
    | functionType
    | constructorType
    // | unionType
	;


// Intentionally ignoring parenthesised types because we shouldn't need to output them
primaryType
options
{
	k = 4 ;
	backtrack = true;
}
    : (predefinedType
    | typeReference
    | objectType
    | tupleType
    | typeQuery)
    arrayIndicator*
    ;

arrayIndicator
    : (LBRACK RBRACK) -> ARRAY_TYPE
    ;
    
// $<	Predefined Types (3.7.1)

predefinedType
    : ANY
    | NUMBER
    | BOOLEAN
    | STRING
    | VOID
    ;

// $>

// $<	Type References (3.7.2)

typeReference
    : typeName { PromoteEOL(null); } typeArguments? -> ^(TYPEREF typeName typeArguments?)
    ;

typeName
    : Identifier
    | qName
    ;

qName
	@init { string result = ""; }
	: x=Identifier { result = $x.text; } ( DOT { result += '.'; } y=Identifier { result += $y.text; } )+ -> Identifier[null, result]
	;
// $>
// $<	3.7.3 Object Type Literals

objectType
    : LBRACE (typeMember semic)* RBRACE -> ^(OBJECT_TYPE typeMember*)
    ;

typeMember
    : propertySignature
    | callSignature
    | constructSignature
    | indexSignature
    | methodSignature
    ;
// $>

// $<	3.7.5 Tuple Type Literals

// Limit to non-tuple types for LL parser friendlyness
tupleType
    : LBRACK nonTupleType (COMMA type)* RBRACK -> ^(TUPLE_TYPE nonTupleType type*)
    ;

// $>

// $<	3.7.6 Union Type Literals

unionType
    : primaryType (OR primaryType)+
    ;

// $>

// $<	3.7.7 Function Type Literals

functionType
    : typeParameters? LPAREN parameterList? RPAREN ARROW type -> ^(FUNC_TYPE typeParameters? parameterList? type?)
    ;

// $>

// $<	3.7.8 Constructor Type Literals

constructorType
    : NEW typeParameters? LPAREN parameterList? RPAREN ARROW type -> ^(CONST_TYPE typeParameters? parameterList? type?)
    ;

// $>

// $<	3.7.9 Type Queries

typeQuery
    : TYPEOF^ (Identifier | qName)
    ;
    
// $>

// $<	3.8 Specifying Members

// $<	3.8.1 Property Signatures

propertySignature
    : propertyName QUE? typeAnnotation? -> ^(PROP_SIG propertyName QUE? typeAnnotation?)
    ;

// $>

// $<	3.8.2 Call Signatures

callSignature
    : typeParameters? LPAREN parameterList? RPAREN typeAnnotation? -> ^(CALL_SIG typeParameters? parameterList? typeAnnotation?)
    ;

// $>

// $<	3.8.2.2 Parameter List

parameterList
    : parameter ( COMMA parameter )* (COMMA restParameter)? -> ^(PARAMS parameter* restParameter?)
    | restParameter -> ^(PARAMS restParameter)
    ;

accessibilityModifier
    : PUBLIC
    | PRIVATE
    | PROTECTED
    ;

parameter
    : accessibilityModifier? Identifier QUE? typeAnnotation?( ASSIGN assignmentExpression )? -> ^(PARAM Identifier accessibilityModifier? QUE? typeAnnotation? assignmentExpression?)
    ;

restParameter
    : ELLIPSIS Identifier typeAnnotation? -> ^(ELLIPSIS Identifier typeAnnotation?)
    ;

// $>

// $<	3.8.3 Construct Signature
constructSignature
    : NEW typeParameters? LPAREN parameterList? RPAREN typeAnnotation? -> ^(CONST_SIG typeParameters? parameterList? typeAnnotation?)
    ;
// $>

indexSignature
options
{
	k = 4 ;
}
    : LBRACK Identifier COLON STRING RBRACK typeAnnotation -> ^(INDEX_SIG Identifier STRING typeAnnotation)
    | LBRACK Identifier COLON NUMBER RBRACK typeAnnotation -> ^(INDEX_SIG Identifier NUMBER typeAnnotation)
    ;

methodSignature
    : propertyName QUE? typeParameters? LPAREN parameterList? RPAREN typeAnnotation? -> ^(METH_SIG propertyName QUE? typeParameters? parameterList? typeAnnotation?)
    ;

// $<	3.9 Type Aliases

typeAliasDeclaration
    : TYPE Identifier EQ type semic
    ;

// $>


typeAnnotation
    : COLON type -> ^(COLON type)
    ;

// $<	5 Interfaces

interfaceDeclaration
	: INTERFACE Identifier typeParameters? extendsClause? LBRACE (typeMember semic)* RBRACE -> ^(INTERFACE Identifier typeParameters? extendsClause? typeMember*)
	;
	
extendsClause
	: EXTENDS typeReference (COMMA typeReference)* -> ^(EXTENDS typeReference*)
	;

// $>

// $<   6 Classes

classDeclaration
	: CLASS Identifier typeParameters? extendsClause? implementsClause? LBRACE classBody RBRACE
	;
	
implementsClause	
	: IMPLEMENTS Identifier (COMMA Identifier)*
	;

classBody
	: classElement*
	;
	
classElement
options
{
	k = 4 ;
}
	: constructorDeclaration 
	| memberVariableDeclaration 
	| memberFunctionDeclaration 
	| memberAccessorDeclaration 
	| indexSignature 
	;
		
constructorDeclaration
	: constructorCallSignature (semic constructorCallSignature)* functionBody
	;
	
constructorCallSignature
	: accessibilityModifier? CONSTRUCTOR LPAREN parameterList RPAREN
	;
	
memberVariableDeclaration
	: accessibilityModifier? STATIC? Identifier typeAnnotation? ( ASSIGN^ assignmentExpression )?
	;
	
memberFunctionDeclaration	
	: memberFunctionCallSignature (semic memberFunctionCallSignature)* functionBody
	;
	
memberFunctionCallSignature
	: accessibilityModifier? STATIC? Identifier LPAREN parameterList RPAREN
	;
	
memberAccessorDeclaration
	: accessibilityModifier? STATIC? getAccessor
	| accessibilityModifier? STATIC? setAccessor
	;
	
getAccessor
	: GET Identifier LPAREN RPAREN typeAnnotation? functionBody
	;
	
setAccessor
	: SET Identifier LPAREN Identifier typeAnnotation? RPAREN typeAnnotation functionBody
	;

// $>


// $<   7 Enums
enumDeclaration	
	: CONST? ENUM Identifier LBRACE enumBody RBRACE
	;
	
enumBody
	: (enumMember COMMA)* enumMember
	;

enumMember
	: Identifier( ASSIGN^ assignmentExpression )?
	;

// $>

// $<   8 Internal Modules

moduleDeclaration
	: MODULE qName LBRACE (sourceElement (COMMA sourceElement)*)? RBRACE
	;
	
importDeclaration
	: IMPORT Identifier ASSIGN qName
	;

exportDeclaration
    : EXPORT exportableElement
    ;

exportableElement
    : interfaceDeclaration
    | classDeclaration
    | moduleDeclaration
    | importDeclaration
    | enumDeclaration
    ;

ambientDeclaration
    : EXPORT ambientElement
    ;

ambientElement
    : interfaceDeclaration
    | ambientClassDeclaration
    | moduleDeclaration
    | importDeclaration
    | enumDeclaration
    ;

ambientClassDeclaration
	: CLASS Identifier typeParameters? extendsClause? LBRACE (typeMember semic)* RBRACE -> ^(CLASS Identifier typeParameters? extendsClause? typeMember*)
	;

// $>

// $>